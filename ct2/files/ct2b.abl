MODULE SYL    "CT2 Rev B - 23/3/2000  


"INPUTS"
a0,a01,a02,a03					pin 78,79,80,81;
a10..a15					pin 73,72,70,69,67,59;
a16..a23					pin 57,56,54,53,48,47,45,44;
a24..a31					pin 42,41,36,35,33,32,30,29;
as,asf,rw,siz0,siz1,fc0,fc1,fc2			pin 5,96,84,8,92,93,7,58;
bg,bgk,dtk,sw					pin 71,23,18,22;
clkd,clk32,clk50				pin 11,28,90;
		
"OUTPUTS"
dsk1,uds,lds					pin 20,17,19;
asdis,dbdis,bgi,flcs,floe,flwe,fpu,dspcs	pin 97,91,3,31,34,21,40,4;
avec,mclk					pin 46,98;
eck,dreco,dsk0					pin 83,68,9 istype 'reg_d,buffer,keep';
ird,iwr						pin 95,94;

"INPUTS/OUTPUTS"
edtk						pin 43;

"NODES"
flh,ct2,fadd,xffx,acia,io,ide,dsp		node istype 'keep';
p16,acdtk					node istype 'keep';
eck3,eck1,eck0					node istype 'reg_d,buffer,keep';
drbt,drdsp,dride,drflh				node istype 'reg_d,buffer,keep';
das1,das2,das3,das4,das5,das6,das7		node istype 'reg_d,buffer,keep';
dk2,dk1,dl1,dl0,mclk2				node istype 'reg_d,buffer,keep';
			
"SETS"
qdr	= [drbt,dride,drdsp,dreco,drflh];
qeck	= [eck3,eck,eck1,eck0];
qdas	= [das1,das2,das3,das4,das5,das6,das7];
qdl	= [dl0,dl1];

"PROPERTIES"
PLSI PROPERTY 'PRESERVE flh';
PLSI PROPERTY 'PRESERVE ct2';
PLSI PROPERTY 'PRESERVE fadd';
PLSI PROPERTY 'PRESERVE xffx';
PLSI PROPERTY 'PRESERVE acia';
PLSI PROPERTY 'PRESERVE dsp';
PLSI PROPERTY 'PRESERVE io';
PLSI PROPERTY 'PRESERVE ide';
PLSI PROPERTY 'PRESERVE fpu';
PLSI PROPERTY 'PRESERVE flcs';
PLSI PROPERTY 'PRESERVE p16';
PLSI PROPERTY 'PRESERVE acdtk';
PLSI PROPERTY 'PRESERVE dbdis';
PLSI PROPERTY 'PRESERVE asdis';

PLSI PROPERTY 'PULLUP ird';
PLSI PROPERTY 'PULLUP iwr';
PLSI PROPERTY 'PULLUP siz0';
PLSI PROPERTY 'PULLUP siz1';
PLSI PROPERTY 'PULLUP bg';
PLSI PROPERTY 'PULLUP sw';

PLSI PROPERTY 'USE_GLOBAL_RESET ON';
PLSI PROPERTY 'ISP ON';
PLSI PROPERTY 'SECURITY ON';


EQUATIONS

"-- DECODING -----------------------------------------------------------------------------------
fadd	= !a31 & !a30 & !a29 & !a28 & !a27 & !a26 & !a25 & !a24		"$00xxxxxx	 FALCON
        #  a31 &  a30 &  a29 &  a28 &  a27 &  a26 &  a25 &  a24;	"$FFxxxxxx
ide	= fadd & a23 & a22 & a21 & a20 & !a19 & !a18 & !a17 & !a16;	"$FFF0xxxx   	 IDE
xffx	= a23 & a22 & a21 & a20 & a19 & a18 & a17 & a16;		"$xxFFxxxx
io	= !fc1 &  fc0 & fadd & a23 & a22 & a21 & a20 & a19 & !a18 & a17	"$FFFAxxxx-BFFFF CART.
	#  fc1 & !fc0 & fadd & a23 & a22 & a21 & a20 & a19 & !a18 & a17
	# !fc1 & fadd & xffx & a15 & !a14 & !a13 & !a12 & !a11 & !a10	"$FFFF80xx- 83FF VID
	# !fc1 & fadd & xffx & a15 & !a14 & !a13 & !a12 & a11;		"$FFFF88xx- 8FFF YM/SCC
dsp	= !fc1 & fadd & xffx & a15 & !a14 &  a13 & !a12 & !a11 & !a10;	"$FFFFA0xx	 DSP
acia	= !as & !fc1 & fadd & xffx & a15 & a14 & a13 & a12 & a11 & a10;	"$FFFFFCxx- FFFF ACIA
flh	= a31 & a30 & a29 & a28 & !a27 & a26 & !a25 & !a24;		"$F4xxxxxx	 FLASH	
ct2 	= !as & a31 & a30 & a29 & a28 & !a27 & a26 &  a25 & !a24;	"$F6xxxxxx	 CT2
!fpu	= !as & das1.q &fc2&fc1&fc0&!a19&!a18&a17&!a16&!a15&!a14&a13;	"FPU

"-- ARBITRATION / BUS ACCESS / INT ------------------------------------------------------------
!bgi	= !bg & as;						"BG to F30 if not AS
	
dbdis   = !bgk 							"SDMA/BLITTER
	# !as & !a31 & !a30 & !a29 & !a28 & !a27 &  a26		"FAST 64 MB $04-07
	# !as & !a31 & !a30 & !a29 & !a28 &  a27 & !a26		"FAST 64 MB $08-0B
	# !as &  a31 &  a30 &  a29 &  a28 & !a27 &  a26		"FLH/CT2    $F4-F7
	# !fpu							"FPU
	# sw & !drbt.q;						"BOOT Turbo

asdis	= !bgk							"SDMA/BLITTER
	# !a31 & !a30 & !a29 & !a28 & !a27 &  a26		"FAST 64 MB $04-07
	# !a31 & !a30 & !a29 & !a28 &  a27 & !a26		"FAST 64 MB $08-0B
	#  a31 &  a30 &  a29 &  a28 & !a27 &  a26		"FLH/CT2    $F4-F7
	# sw & !rw & io & dl1.q					"IO    Turbo
	# sw & !drbt.q;						"BOOT  Turbo

!avec	= !as & fc2 & fc1 & fc0 & xffx & !a03 &  a02 & !a01  	"AVEC 2 = HBL
	# !as & fc2 & fc1 & fc0 & xffx &  a03 & !a02 & !a01; 	"AVEC 4 = VBL
		 
uds.oe	= bgk;
!uds	= fadd & !as & rw    			
	# fadd & !as & !a0;
lds.oe  = bgk;
!lds	= fadd & !as & rw 	
	# fadd & !as & a0	
	# fadd & !as & !siz0 
	# fadd & !as &  siz1;

"-- ACIA : DTacK & Clock E --------------------------------------------------
qeck.clk= !mclk2.q;
eck0.d	= !eck1.q & !eck.q
	#  eck1.q &  eck.q;
eck1.d	=  eck0.q & !eck.q
	# !eck0.q & eck1.q;
eck.d	=  eck0.q &  eck.q
	# !eck0.q & eck1.q
	# !eck0.q & eck1.q & !acia
	# !eck0.q & eck1.q & !eck3.q;
eck3.d	= !acia;
p16	= acia & eck0.q & !eck.q
	# acia & eck1.q & !eck.q & eck3.q
	# acia & eck1.q & p16
	# p16 & !as;			
!acdtk	= acia & !eck1.q & p16 & eck.q	
	# !acdtk & !as;

"-- FLASH -------------------------------------------------------------------
!flcs   = !as & fadd & !drbt.q & rw & sw	"Turbo BOOT
	# !as & flh;				
!floe   = !as & fadd & !drbt.q & rw & sw	"Turbo BOOT
	# !as & flh &  rw;		
!flwe	= !as & flh & !rw & drflh.q;

"-- IDE ---------------------------------------------------------------------
!ird	= !sw & ide &  rw & !as & das1.q 			"Normal R
	#  sw & ide &  rw & !as & das2.q & !dride.q		"Turbo  R	
	#  sw & ide &  rw & !as & das3.q &  dride.q;		"Turbo  R WS

!iwr	= !sw & ide & !rw & !as & das1.q			"Normal W
	#  sw & ide & !rw & !as & das2.q & !das5.q & !dride.q 	"Turbo  W
	#  sw & ide & !rw & !as & das3.q & !das6.q &  dride.q;  "Turbo  W WS

"-- DSP ---------------------------------------------------------------------
!dspcs	= !sw & dsp &  rw & !as & das1.q			"Normal R
	# !sw & dsp & !rw & !as & das1.q & !das2.q		"Normal W	
	#  sw & dsp &  rw & !as & das4.q 			"Turbo  R   das4   le 3/7/99    
	#  sw & dsp & !rw & !as & das3.q & !das6.q & !drdsp.q	"Turbo  W   das4,7 le 1/2/00 
  	#  sw & dsp & !rw & !as & das4.q & !dl1.q  &  drdsp.q;	"Turbo  W WS
	
"-- DSACK0 - DSACK1 - EDTACK -----------------------------------------------
dsk0.oe = fpu;
dsk0.clk= clk50;
!dsk0.d	= !sw & dsp & !as & das2.q		"Normal DSP
	#  sw & dsp & !as & das7.q & !drdsp.q	"Turbo  DSP	
	#  sw & dsp & !as & dl1.q  &  drdsp.q	"Turbo  DSP WS
	# !flcs & !as & das6.q			"FLASH
	# ct2 & !as;				"CT2
 				
dsk1.oe = fpu;
!dsk1	= !sw & !as & !acdtk			"Normal ACIAs
	# !sw & !as & !edtk 	 		"Normal SDMA/MFP
	# !sw & !as & !dtk			"Normal COMBEL
	#  sw & !as & dk2.q 			"Turbo
	#  sw & !as & das4.q & ide & !dride.q 	"Turbo IDE FAST - 6 cycles
	#  sw & !as & das6.q & ide &  dride.q;	"Turbo IDE SLOW - 8 cycles 

dk2.clk	= !clk50;
dk2.ar	= as;
dk2.d	= dk1.q & !as;

dk1.clk = !clk50;
dk1.ar  = as;
dk1.d	= !as & das2.q & !acdtk			"Turbo ACIAs
	# !as & das2.q & !edtk			"Turbo SDMA/MFP 
	# !as & das2.q & !dtk & !io & !ide	"Turbo COMBEL (NO IO & IDE)
	# !as & dl1.q & io;			"Turbo IO

edtk.oe = !bgk;				    	"SDMA Master
!edtk   = !sw & !asf & !dtk 
	#  sw & !asf & das1.q & !dtk &  rw
	#  sw & 	        !dtk & !rw;	"das1-->ok			        	

qdas.ar	= as & asf;
qdas.clk= !sw & !mclk2.q 	 	"16 MHz
	#  sw & !clk50;			"50 MHz
das1.d	= !as  &  bgk
	# !dtk & !bgk;
das2.d	= das1.q;
das3.d	= das2.q;
das4.d	= das3.q;
das5.d	= das4.q;
das6.d	= das5.q;
das7.d  = das6.q;

qdl.clk	= clkd;				"Delay with 2MHz clock
qdl.ar 	=  as;	 					
dl0.d	= !as; 		
dl1.d 	= !as & dl0.q;

"-- CLOCKs ------------------------------------------------------------
mclk	= clk32 & !sw			"Clock 32/50 for COMBEL-DSP
	# clk50 &  sw;

mclk2.clk= clk32 & !sw			"Clock 16/25 (Combel emulation)
	 # clk50 &  sw;
mclk2.d	= !mclk2.q;

"-- DATA REGISTERs ----------------------------------------------------
qdr.clk = clk50;	
drflh.d	= !ct2 & drflh.q		"A18 FLASH WRITE ENABLE	
	#  ct2 & !rw & a18;
dreco.d	= !ct2 & dreco.q		"A20 EXTCLK OFF
	#  ct2 & !rw & a20;
dride.d	= !ct2 & dride.q		"A21 WS on IDE
	#  ct2 & !rw & a21;
drdsp.d	= !ct2 & drdsp.q		"A22 WS on DSP
	#  ct2 & !rw & a22;
drbt.d	= drbt.q			"A23 FALCON ENABLE
	#  ct2 & !rw & a23;

end;
